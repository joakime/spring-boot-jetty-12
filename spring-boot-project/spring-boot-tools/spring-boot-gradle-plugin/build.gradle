buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
	}
	dependencies {
		classpath("io.spring.javaformat:spring-javaformat-gradle-plugin:0.0.15")
	}
}

plugins {
	id 'java-library'
	id 'eclipse'
}

apply plugin: 'io.spring.javaformat'

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	api "io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE"
	implementation localGroovy()
	implementation gradleApi()
	implementation project(":spring-boot-loader-tools")
	testImplementation gradleTestKit()
	testImplementation "org.apache.commons:commons-compress:1.13"
}

jar {
	manifest {
		attributes 'Implementation-Version': (version ? version : 'unknown')
	}
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

javadoc {
	options {
		author()
		stylesheetFile = file('src/main/javadoc/spring-javadoc.css')
		links = [
			'https://docs.oracle.com/javase/8/docs/api/',
			'https://docs.gradle.org/current/javadoc/'
		]
		source = '8'
	}
	title = "${project.description} $version API"
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
	classifier = "javadoc"
	from javadoc
}

artifacts {
	archives sourcesJar
	archives javadocJar
}
